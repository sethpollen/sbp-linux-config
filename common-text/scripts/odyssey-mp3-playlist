#!/usr/bin/env fish
#
# Takes as input a bunch of .zip files from my "Adventures in Odyssey" Google
# Drive folder. Sorts and flattens their contents, and intersperses periods of
# silence. The result is a set of files suitable for copying to a cheap MP3
# player.
#
# How to use:
#
#   1) rm -rf ~/Downloads/*
#
#   2) Open
#      https://drive.google.com/drive/u/1/folders/11_Sc9Mn2v7GaaoBFMbC13OEq7RDw8R4y
#      in a browser.
#
#   3) Right-click on "Albums" in the top breadcrumb bar. Click "Download".
#      Wait for all the downloads to complete.
#
#   4) cd ~/Downloads && odyssey-mp3-playlist *.zip
#
#   5) "~/Downloads/Playlists" now contains a flat set of files which can be
#      copied to the MP3 player.

# Extract the .zip files. This will produce files under ~/Downloads/Albums.
for zip in $argv
  unzip $zip
  or exit
end

set albumsDir ./Albums

# Make an output directory.
set playlistsDir ./Playlists
mkdir -p $playlistsDir
or exit

# Fetch a silence track.
set silenceFile ./silence.mp3
curl \
  https://raw.githubusercontent.com/sethpollen/sbp_linux_config/master/cookbooks/26-hours-of-silence.mp3 \
  --output $silenceFile
or exit

for album in (ls $albumsDir)
  set episodes (find -wholename "$albumsDir/$album/*.mp3" | sort)

  # Group episodes into playlists of 2 each.
  set counter 1
  while test (count $episodes) -gt 0
    set myEpisodes $episodes[1..2]

    # Include a counter to force the silence tracks to interleave with the
    # audio tracks.
    set outfile (printf '%s/%s_%02d' $playlistsDir $album $counter)
    set counter (math $counter + 1)

    for episode in $myEpisodes
      set episodeName (basename $episode .mp3)
      set outfile {$outfile}__$episodeName
    end
    set outfile {$outfile}.mp3

    echo '##' Building $outfile
    sox $myEpisodes $outfile
    or exit

    set silenceOutfile (printf '%s/%s_%02d_silence.mp3' $playlistsDir $album $counter)
    set counter (math $counter + 1)
    cp $silenceFile $silenceOutfile

    set episodes $episodes[3..-1]
  end
end

# Clean up.
#rm $silence
